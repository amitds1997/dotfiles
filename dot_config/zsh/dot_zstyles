#!/usr/bin/env zsh

# Antidote specific options
zstyle ':antidote:plugin:*' defer-options '-p'
zstyle ':antidote:*' zcompile 'yes'

# Zephyr
zstyle ':zephyr:plugin:*' 'use-cache' yes
zstyle ':zephyr:plugin:editor' dot-expansion 'yes'
zstyle ':zephyr:plugin:editor' symmetric-ctrl-z 'yes'

# Editor
zstyle ':zle:edit-command-line' editor nvim

# Case-insensitive match if there are no case-sensitive matches
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}'

# Some functions can be slow to complete, so we use a cache to speed them up
zstyle ':completion:*' use-cache yes
zstyle ':completion:*' cache-path "${XDG_CACHE_HOME:-$HOME/.cache}/zsh/zcompcache"
[[ -d "${XDG_CACHE_HOME:-$HOME/.cache}/zsh" ]] || mkdir -p "${XDG_CACHE_HOME:-$HOME/.cache}/zsh"

# Group matches and describe
zstyle ':completion:*:matches' group 'yes'
zstyle ':completion:*:options' description 'yes'
zstyle ':completion:*:options' auto-description '%d'

# Do not include pwd after ../
zstyle ':completion:*' ignore-parents parent pwd

# Divide man pages into sections
zstyle ':completion:*:manuals' separate-sections true

# Hide zsh internal functions and preexec/precmd hooks
zstyle ':completion:*:functions' ignored-patterns '(_*|pre(cmd|exec))'

# Array completion element sorting
zstyle ':completion:*:*:-subscript-:*' tag-order indexes parameters

# Handle directory listings
zstyle ':completion:*:*(cd|z):*' tag-order local-directories directory-stack path-directories
zstyle ':completion:*:-tilde-:*' group-order 'named-directories' 'path-directories' 'users' 'expand' # Handle `~` expansion order
zstyle ':completion:*' squeeze-slashes true # Convert multiple // to a single one

# fzf-tab related configuration
zstyle ':completion:*:git-checkout:*' sort false # disable sort when completing `git checkout`
zstyle ':completion:*:descriptions' format '[%d]' # set descriptions format to enable group support
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS} # Set list-colors to enable filename colorizing
zstyle ':completion:*' menu no # force zsh not to show completion menu, which allows fzf-tab to capture the unambiguous prefix
zstyle ':fzf-tab:*' switch-group '<' '>' # switch group using `<` and `>`
zstyle ':fzf-tab:*' fzf-flags --border-label=' Auto complete/suggestion(s) ' --preview-label=' Preview ' # Noice flags
zstyle ':fzf-tab:*' use-fzf-default-opts yes # Make fzf-tab follow FZF_DEFAULT_OPTS
zstyle ':fzf-tab:*' prefix '' # No dot prefix on completion items

# Add a rich git log preview for git checkout, switch, and branch commands
zstyle ':fzf-tab:complete:git-(checkout|switch|branch):*' fzf-preview \
	'git log --color=always --oneline --graph --date=short --pretty="format:%C(auto)%cd %h%d %s" $word | head -200'

# Use `eza` to show previews for directories, `bat` for files and file info for the rest
zstyle ':fzf-tab:complete:(\|*/|)(ls|bat|cat|cd|rm|cp|mv|ln|nano|nvim|vi|vim|code|open|head|tail|tree|source|z):*' fzf-preview \
'[[ -d $realpath ]] && \
  eza -1 --color=always --icons=always --group-directories-first "$realpath" | head -n 100 || \
  {
    mime=$(file -Lb --mime-type "$realpath")
    if [[ $mime == text/* ]]; then
      bat --style=plain --color=always --pager=never --line-range :100 "$realpath"
    else
      # Fallback: gather info and format via bat
      {
        echo "$realpath"
        file -Lb "$realpath"
      } | bat --style=plain --color=always --pager=never
    fi
  }'

# Show package information when using homebrew install/uninstall/search/info
zstyle ':fzf-tab:complete:brew-(install|uninstall|search|info):*-argument-rest' fzf-preview 'HOMEBREW_COLOR=1 brew info $word'

# Use `tldr` for tldr and command information, if available
zstyle ':fzf-tab:complete:tldr:argument-1' fzf-preview 'tldr --color always $word'
zstyle ':fzf-tab:complete:-command-:*' fzf-preview \
  '(out=$(tldr --color always "$word") 2>/dev/null && echo $out) || (out=$(MANWIDTH=$FZF_PREVIEW_COLUMNS man "$word") 2>/dev/null && echo $out) || (out=$(which "$word") && echo $out) || echo "${(P)word}"'

# Give a nice output when trying to find processes to kill
zstyle ':completion:*:*:*:*:processes' command "ps -u $USER -o pid,user,comm -w -w"
zstyle ':fzf-tab:complete:(kill|ps):argument-rest' fzf-preview '[[ $group == "[process ID]" ]] && ps -o args= -p $word -ww'
zstyle ':fzf-tab:complete:(kill|ps):argument-rest' fzf-flags --preview-window=down:3:wrap

# vim: ft=zsh sw=2 ts=2 et