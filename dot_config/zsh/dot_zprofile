#!/usr/bin/env zsh

# https://specifications.freedesktop.org/basedir-spec/basedir-spec-latest.html
export XDG_CONFIG_HOME=${XDG_CONFIG_HOME:-$HOME/.config}
export XDG_CACHE_HOME=${XDG_CACHE_HOME:-$HOME/.cache}
export XDG_DATA_HOME=${XDG_DATA_HOME:-$HOME/.local/share}
export XDG_STATE_HOME=${XDG_STATE_HOME:-$HOME/.local/state}
export XDG_RUNTIME_DIR=${XDG_RUNTIME_DIR:-$HOME/.xdg}

for xdgdir in XDG_{CONFIG,CACHE,DATA,STATE}_HOME XDG_RUNTIME_DIR; do
  [[ -e ${(P)xdgdir} ]] || mkdir -p ${(P)xdgdir}
done

# Browser
if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER="${BROWSER:-open}"
fi

# Editor
export EDITOR="${EDITOR:-nvim}"
export VISUAL="${VISUAL:-nvim}"
export PAGER="${PAGER:-less}"

# Regional settings
export LANG="${LANG:-en_US.UTF-8}"

# Set paths where zsh will search for commands
path=(
  $HOME/{,s}bin(N)
  /{usr,opt}/local/{,s}bin(N)
  $HOME/{.local,.cargo,go}/bin
  $HOME/.local/share/bob/nvim-bin
  $path
)

if [[ $(uname) == "Darwin" ]]; then
    path=(
        /opt/homebrew/{,s}bin(N)
        /usr/local/opt/llvm/bin
        /opt/homebrew/opt/postgresql@17/bin
        $path
    )
fi

# Ensure path arrays do not contain duplicates
typeset -gU path fpath mailpath

# Set default options for `less`
export LESS="${LESS:--g -i -M -R -S -w -z-4}"

# Use Neovim as pager for man pages. Do not set it to vim or vi
# as that does not work
export MANPAGER='nvim +Man!'
