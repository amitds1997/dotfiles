# If we need to profile ZSH, start it now
[[ ${ZPROFRC:-0} -eq 0 ]] || zmodload zsh/zprof

# These operations write to stdout so should be run before starting instant prompt
command -v fnm &> /dev/null && eval "$(fnm env --use-on-cd)"

# Load p10k instant prompt {{{
# Use p10k instant prompt to load the prompt while rest of the config loads in the background.
# All console inputs should go above this block. This is disabled if we run zsh profiling using `zprofrc` alias.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" && ${ZPROFRC:-0} -eq 0 ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi
# }}}

# Sane defaults for ZSH. Get a working shell even if the later configuration gets borked.
[[ -f $ZDOTDIR/plugins/essentials/essentials.plugin.zsh ]] && . $ZDOTDIR/plugins/essentials/essentials.plugin.zsh

# Plugin manager {{{

# Install the plugin manager - Antidote (if not present) {{{
ANTIDOTE_HOME=$ZDOTDIR/plugins/.external
ZSH_PLUGINS_FILE=${ZDOTDIR:-~}/.zplugins.zsh
[[ -e $ZDOTDIR/.antidote ]] ||
  git clone --depth=1 https://github.com/mattmc3/antidote.git $ZDOTDIR/.antidote
# }}}

# Install any missing plugins {{{
[[ -f ${ZSH_PLUGINS_FILE:r}.txt ]] || touch ${ZSH_PLUGINS_FILE:r}.txt
[[ -e $ZDOTDIR/plugins/.external ]] || antidote load $ZDOTDIR/.zplugins.txt
# }}}

# Bundle all installed plugins and source them {{{
if [[ ! $ZSH_PLUGINS_FILE -nt ${ZSH_PLUGINS_FILE:r}.txt ]] || [[ ! -s $ZSH_PLUGINS_FILE ]]; then
  source $ZDOTDIR/.antidote/antidote.zsh
  (envsubst < ${ZSH_PLUGINS_FILE:r}.txt | antidote bundle >| $ZSH_PLUGINS_FILE)
fi
source $ZSH_PLUGINS_FILE
# }}}

# Lazy load antidote definition to fpath
fpath+=(${ZDOTDIR:-~}/.antidote/functions)
autoload -Uz $fpath[-1]/antidote

# }}}

# Enable fzf auto completions
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# Source p10k
[[ ! -f $XDG_CONFIG_HOME/zsh/.p10k.zsh ]] || source $XDG_CONFIG_HOME/zsh/.p10k.zsh

# Load zstyles (ZSH configuration custom params)
[[ -f $ZDOTDIR/.zstyles ]] && source $ZDOTDIR/.zstyles

# Any machine-specific ZSH configuration goes here (Last thing to load)
[[ -f $ZDOTDIR/.zshrc_local ]] && source $ZDOTDIR/.zshrc_local

# Done profiling.
[[ ${ZPROFRC:-0} -eq 0 ]] || { unset ZPROFRC && zprof } || true

# vim: ft=zsh:sw=2:ts=2:foldmethod=marker
